// https://docs.gradle.org/current/userguide/groovy_plugin.html
apply plugin: 'groovy'

// https://docs.gradle.org/current/userguide/codenarc_plugin.html
apply plugin: 'codenarc'

// https://docs.gradle.org/current/userguide/idea_plugin.html
apply plugin: 'idea'

// https://plugins.gradle.org/plugin/com.github.johnrengelman.shadow
apply plugin: 'com.github.johnrengelman.shadow'

// https://docs.gradle.org/current/userguide/organizing_build_logic.html#sec:external_dependencies
buildscript {
    repositories {
        maven {
	    url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

// https://docs.gradle.org/current/userguide/artifact_dependencies_tutorial.html#N10621
repositories {
    mavenCentral()
    // flatDir {
    //     dirs 'bin'
    // }
}

// https://docs.gradle.org/current/userguide/artifact_dependencies_tutorial.html#N105E1
dependencies {
    testCompile "junit:junit:4.12"
    testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    compile "org.codehaus.groovy:groovy:${groovyVersion}"
    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    // compile "io.dropwizard:dropwizard-jdbi:${dropwizardVersion}"
    // compile ':ojdbc6_g:'
}

codenarc {
    configFile = new File("config/codenarc/codenarc.groovy")
}

// https://docs.gradle.org/current/userguide/java_plugin.html#N12B9A
// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Jar.html
jar {
    appendix = System.currentTimeMillis()
    version = 'git log -n1 --format=format:%h'.execute().getText()
}

// https://github.com/johnrengelman/shadow
shadowJar {
    appendix = jar.appendix
    version = jar.version
    manifest {
        attributes 'Main-Class': "${mainClass}"
    }
    mergeServiceFiles()
}

build.dependsOn shadowJar

// FIXME: use ShadowJar (java -jar build/libs/web-api-skeleton-all.jar server configuration.yaml)
task run(type: JavaExec, dependsOn: build) {
    main = "${mainClass}"
    classpath = sourceSets.main.runtimeClasspath
    args = ['server', 'configuration.yaml']
}
